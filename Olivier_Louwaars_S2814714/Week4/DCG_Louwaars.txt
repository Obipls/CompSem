%% Sentence level
s --> np(A),vp(decl,A). % the cat eats + ...
s --> np(A),pp,vp(decl,A). % the cat on the street eats + ...
s --> [there], be(A), np(A). % there are cats, there is a cat
s --> [there], be(A), np(A),pp. % there are cats on the street
 
%% Preposition level
pp --> prep,np(_). % on   the street
 
%% Noun phrase group?
% agreement with verb needed:
np(A) --> n(A). % tea, milk, dogs
np(A) --> a(pos),n(A). % brown dogs, old milk, hot tea
 
np(A) --> det(A),n(A). % a dog, two dogs
np(A) --> det(A),a(pos),n(A). % a brown dog, two brown dogs
 
% no agreement with verb needed:
np --> n(_). % dogs, tea, milk
np --> a(pos),n(_). % brown dogs, old milk, hot tea 
np --> det(A),n(A). % a dog, two dogs
np --> det(A),a(pos),n(A). % a brown dog, two brown dogs
   
%% Verb level
% Without object (zonder lijdend voorwerp)
vp(decl,A) --> iv(decl,A). % sleeps
vp(decl,A) --> iv(decl,A),pp. % sleeps on a couch
vp(decl,A) --> tv(decl,A). % drinks
vp(decl,A) --> tv(decl,A),pp. % drinks on a couch
vp(decl,A) --> dtv(decl,A). % writes
vp(decl,A) --> dtv(decl,A),pp. % writes on a couch
 
% With object (met lijdend voorwerp)
vp(decl,A) --> tv(decl,A),np. % drinks milk
vp(decl,A) --> tv(decl,A),np,pp. % drinks on a couch
vp(decl,A) --> dtv(decl,A),np. % writes a letter
vp(decl,A) --> dtv(decl,A),np,pp. % writes a letter on a couch
 
% With two objects (met meewerkend voorwerp en lijdend voorwerp)
vp(decl,A) --> dtv(decl,A),np,np. % writes the boy a card
vp(decl,A) --> dtv(decl,A),np,np,pp. % writes the boy a card on the couch
 
% Replace 'sleeps' by 'is sleeping'  
iv(decl,A) --> be(A),iv(prep). % is sleeping (instead of sleeps)
tv(decl,A) --> be(A),tv(prep). % is drinking (instead of drinks)
dtv(decl,A) --> be(A),dtv(prep). % is writing (instead of writes)
  
% Passive sentences
vp(decl,A) --> be(A),tv(pstp),[by],np. % is chased by a dog
vp(decl,A) --> be(A),tv(pstp),[by],np,pp. % is chased by a dog on the street
vp(decl,A) --> be(A),dtv(pstp),[by],np. % is written by a man
vp(decl,A) --> be(A),dtv(pstp),[by],np,pp. % is written by a man on the street
 
vp(decl,A) --> be(A),tv(pstp). % is chased
vp(decl,A) --> be(A),tv(pstp),pp. % is chased on the street
vp(decl,A) --> be(A),dtv(pstp). % is written
vp(decl,A) --> be(A),dtv(pstp),pp. % is written on the street
 
vp(decl,A) --> be(A),[being],tv(pstp),[by],np. % is being chased by a dog
vp(decl,A) --> be(A),[being],tv(pstp),[by],np,pp. % is being chased by a dog on the street
vp(decl,A) --> be(A),[being],dtv(pstp),[by],np. % is being written by a man
vp(decl,A) --> be(A),[being],dtv(pstp),[by],np,pp. % is being written by a man on the street
 
vp(decl,A) --> be(A),[being],tv(pstp). % is being chased
vp(decl,A) --> be(A),[being],tv(pstp),pp. % is being chased on the street
vp(decl,A) --> be(A),[being],dtv(pstp). % is being written
vp(decl,A) --> be(A),[being],dtv(pstp),pp. % is being written on the street
   
% Other cases
vp(decl,A) --> be(A),a(pos). % is red, are large
vp(decl,A) --> be(A),a(com),[than],np. % the cat is bigger than...
 
%% Lexical
 
% Verbs
% M: Mood (difference between walking, walk, walked)
% decl: declarative mood (Dutch: persoonsvorm): swims or swim
% base: (Dutch: stam): the infinitive??? 
% prep: present participle (writing)
% pstp: past participle (written)
%   
% ---
% A: Agreement (walks vs walk)
% sg: 3rd person singular (walks)
% pl: plural, so not 3rd person singular (walk)
% We don't need first and second person (I, we, you), so this may be sufficient
 


iv(prep) --> [writing].
iv(decl,sg) --> [writes].
iv(decl,pl) --> [write].
iv(pstp) --> [written].
 
tv(prep) --> [riding].
tv(decl,sg) --> [rides].
tv(decl,pl) --> [ride].
tv(pstp) --> [ridden].
 
dtv(prep) --> [sending].
dtv(decl,sg) --> [sends].
dtv(decl,pl) --> [send].
dtv(pstp) --> [sent].
 
dtv(prep) --> [writing].
dtv(decl,sg) --> [writes].
dtv(decl,pl) --> [write].
dtv(pstp) --> [written].
 
% the verb 'to be'
be(sg) --> [is].
be(pl) --> [are].
 
% noun group?
det(_) --> [the]. % both singular and plural
det(sg) --> [a]. % only singular
n(sg) --> [bicycle].
n(pl) --> [bicycles].
n(sg) --> [man].
n(pl) --> [men].
n(sg) --> [dog].
n(pl) --> [dogs].
 
% Other things
a(pos) --> [large].
a(pos) --> [green].
a(com) --> [larger].
a(com) --> [greener].

prep --> [with].
prep --> [on].
prep --> [under].
 

% Useful commands:
% ['group_VG']. % load group_VG
% make. % something like: reload the file.
% s(X, []). % After using a ; the next possible sentence is displayed.
% G = [_,_,_,_,_,_],s(G,[]). % Gives a sentence of 6 elements: use ; to display a next sentence.
% G = [the,man,_,_,a,_],s(G,[]). % Gives a sentence of 6 elements, with fixed elements.
